plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.firebase.crashlytics'
    id 'kotlin-android-extensions'

}

def getVersionPropertiesValue(def varName) {
    def propertiesFile = file('version.properties')

    if (!propertiesFile.canRead()) {
        throw new FileNotFoundException("Could not read " + propertiesFile.name)

    }

    Properties properties = new Properties()
    properties.load(new FileInputStream(propertiesFile))
    return  properties[varName]
}

android {
    compileSdk 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.cindymb.cibweatherapp"
        minSdk 21
        targetSdk 30
        versionCode Integer.valueOf(getVersionPropertiesValue('VERSION_CODE'))
        versionName Integer.valueOf(getVersionPropertiesValue('VERSION_MAJOR')) + '.' + Integer.valueOf(getVersionPropertiesValue('VERSION_MINOR')) + '.' + Integer.valueOf(getVersionPropertiesValue('VERSION_PATCH'))

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "BASE_URL", "\"http://api.weatherbit.io/v2.0/forecast/daily/\"")
        buildConfigField("String", "APP_KEY", "\"addeb6a5e7c047318d038e47f495db99\"")
    }

    buildTypes {

        release {
            minifyEnabled true //indicates that the code should be shrunk
            shrinkResources true // remove all unused resources
            proguardFile getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFile 'proguard-project.txt' // file shall hold ProGuard configuration
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix = ".debug"
        }
    }

    flavorDimensions "variant"

    productFlavors {
        production {
            multiDexEnabled true
        }
        internal {
            applicationIdSuffix ".internal"
            multiDexEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.3.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // AndroidX - Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:${navigation_version}"
    implementation "androidx.navigation:navigation-ui-ktx:${navigation_version}"

    // AndroidX - Lifecycle
    def lifecycle_version = "2.3.1"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:${lifecycle_version}"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${lifecycle_version}"

    // Dagger
    def dagger_version = "2.35.1"
    implementation "com.google.dagger:dagger:${dagger_version}"
    kapt "com.google.dagger:dagger-compiler:${dagger_version}"

    // Retrofit
    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:${retrofit_version}"
    implementation "com.squareup.retrofit2:converter-gson:${retrofit_version}"

    // Ok Http
    def ok_http_version = "4.9.0"
    implementation "com.squareup.okhttp3:okhttp:${ok_http_version}"
    implementation "com.squareup.okhttp3:logging-interceptor:${ok_http_version}"

    // Kotlin Coroutines
    def coroutines_version = "1.4.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${coroutines_version}"

    //Firebase
    implementation 'com.google.firebase:firebase-crashlytics:18.2.1'
    implementation 'com.google.firebase:firebase-analytics:19.0.1'

    // Timber logger
    implementation "com.jakewharton.timber:timber:4.7.1"

    //Picasso
    def picassoVersion = "2.71828"
    implementation "com.squareup.picasso:picasso:$picassoVersion"

    implementation "org.permissionsdispatcher:permissionsdispatcher-ktx:1.0.1"

}